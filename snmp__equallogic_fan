#!/usr/bin/perl -w
#%# family=snmpauto
#%# capabilities=snmpconf
use strict;
use warnings;
use Munin::Plugin::SNMP;
use Net::SNMP 'oid_lex_sort';

my( $PluginSess, $E ) = Munin::Plugin::SNMP->session();
die sprintf( "# Error: %s\n", $E ) if $E;

# http://cric.grenoble.cnrs.fr/Administrateurs/Outils/MIBS/?oid=1.3.6.1.4.1.12740.2.1.7.1
my $PluginVers = '0.0.6';
my $GraphValue = {
    'title'     => 'Fans Speed',
    'device'    => 'EqualLogic',
    'vlabel'    => 'RPM',
    'base'      => 1000,
    'lower'     => 1500,
    'upper'     => 9000,
};
my $StateIndex = {
    '0' => 'unknown',
    '1' => 'normal',
    '2' => 'warning',
    '3' => 'critical',
};
my $ParentOIDv = '.1.3.6.1.4.1.12740.2.1.7.1';
my $OIDEnfants = {
    'label' => 2,   # eqlMemberHealthDetailsFanName
    'value' => 3,   # eqlMemberHealthDetailsFanValue
    'state' => 4,   # eqlMemberHealthDetailsFanCurrentState
    'crit1' => 5,   # eqlMemberHealthDetailsFanHighCriticalThreshold
    'warn1' => 6,   # eqlMemberHealthDetailsFanHighWarningThreshold
    'crit0' => 7,   # eqlMemberHealthDetailsFanLowCriticalThreshold
    'warn0' => 8,   # eqlMemberHealthDetailsFanLowWarningThreshold
};
my $DeviceMaps = {
    '1' => 'Power-Cooling-Module0-Fan0',
    '2' => 'Power-Cooling-Module0-Fan1',
    '3' => 'Power-Cooling-Module1-Fan0',
    '4' => 'Power-Cooling-Module1-Fan1',
};
my $SNMPConfig = {
    'host'      => $ENV{'host'} || $PluginSess->hostname || '',
    'port'      => $ENV{'port'} || 161,
    'community' => $ENV{'community'} || 'dellpublic',
    'version'   => $ENV{'version'} || '2c',
};

my $MemberOIDv = '.1.3.6.1.4.1.12740.2.1.1.1.9.1';
my $MemberList = $PluginSess->get_by_regex( $MemberOIDv, '\d+' ) || {};
my $MemberName = undef; # My member name, id

if( scalar keys %$MemberList ) {
    # Get my member id string
    for my $e ( keys %$MemberList ) {
        next unless uc $SNMPConfig->{'host'} eq $MemberList->{ $e };
        $MemberName = $e;
        last;
    }
}

my $v = '.1.'.$MemberName;
my $MetricsSet = $PluginSess->get_hash(
                    '-baseoid'  => $ParentOIDv,
                    '-cols'     => {
                        $OIDEnfants->{'label'}.$v => 'label',
                        $OIDEnfants->{'value'}.$v => 'value',
                        $OIDEnfants->{'state'}.$v => 'state',
                        $OIDEnfants->{'crit1'}.$v => 'critical',
                        $OIDEnfants->{'warn1'}.$v => 'warning',
                    }
                 ) or die( '# Error: '.$PluginSess->error );

if( defined $ARGV[0] ) {
    # munin-run snmp_equallogic <some argument>
    if( $ARGV[0] eq 'snmpconf' ) {
        # munin-run snmp__equallogic snmpconf
        printf( "require %s. [2-8]\n", $ParentOIDv );

    } elsif( $ARGV[0] eq 'config' ) {
        # munin-run snmp__equallogic config
        if( $SNMPConfig->{'host'} && $SNMPConfig->{'host'} ne 'localhost' ) {
            printf( "host_name %s\n", $SNMPConfig->{'host'} );
        }
        printf( "graph_title %s %s\n", $GraphValue->{'device'}, $GraphValue->{'title'} );
        printf( "graph_args --lower-limit %d --upper-limit %d --base %d\n",
                $GraphValue->{'lower'}, $GraphValue->{'upper'}, $GraphValue->{'base'} );
        printf( "graph_vlabel %s\n", $GraphValue->{'vlabel'} );
        printf( "graph_category sensors\n" );

        my $o = [ map { oid2label( $_ ) } oid_lex_sort( keys %$MetricsSet ) ];
        printf( "graph_order %s\n", join( ' ', @$o ) );

        for my $e ( keys %$MetricsSet ) {
            my $i = oid2label( $e );
            next unless $i;

            for my $f ( qw|label warning critical| ) {
                printf( "%s.%s %s\n", $i, $f, $MetricsSet->{ $e }->{ $f } );
            }
            printf ( "%s.type GAUGE\n", $i );
        }
    }
    exit 0;

} else {
    # munin-run snmp__equallogic_fan
    for my $e ( keys %$MetricsSet ) {
        my $i = oid2label( $e );
        next unless $i;
        printf( "%s.value %d\n", $i, $MetricsSet->{ $e }->{'value'} );
    }
}

sub oid2label {
    my $argv = shift;
    return $DeviceMaps->{ $argv } || '';
}

=head1 NAME

snmp__equallogic_fan - Munin plugin for monitoring fan RPMs in EqualLogic
storage via SNMP.

=head1 SETUP

    # cat /etc/hosts
    192.168.0.101 eq01
    192.168.0.102 eq02

    # cd /etc/munin/plugins
    # for E in eq01 eq02; do
    > ln -s /usr/share/munin/plugins/snmp__equallogic_fan ./snmp_${E}_equallogic_fan
    > done

    # /etc/rc.d/init.d/munin-node restart

=head1 SEE ALSO

SNMP OIDs for EqualLogic
L<http://cric.grenoble.cnrs.fr/Administrateurs/Outils/MIBS/?oid=1.3.6.1.4.1.12740>

=head1 AUTHOR

Copyright (C) 2014 azumakuniyuki

=head1 LICENSE

This library is free software; you can redistribute it and/or modify it under 
the same terms as Perl itself.

=cut
